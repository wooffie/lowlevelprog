T 56 K [Адрес загрузки]

[adresses]
G K [ Директива, фиксация начального адреса]
T 45 K [ Установка адреса для параметров далее]
P 256 F [45 = H , тут будет адрес первого элемента массива]
P 255 F [46 = N , длинна массива для сортировки]
TZ  [ востановления адреса целевой ячейки]

[sub]
G K  [ Директива, фиксация начального адреса]
[далее в квадратных скобах адреса @ ]
[0] A 3 F [Пролог: формирование кода инструкции возврата]
[1] T 54 [RET] @   [Пролог: запись инструкции возврата]
[2]  A 0 H  [adr = H]   [set adr and len on 52,53]
[3]  U 59 @
[4]  A 0 N  [len = N]
[5]  S [TWO]55 @
[6]  T 60 @
[7]  [START] T 61 @    [self-modified code]
[8]  A [A0]56 @
[9]  A 59 @
[10] U [Z1]22 @
[11] U [W1] 28 @
[12] A [TWO]55 @
[13] T [W2]30 @
[14] A [S1]57 @
[15] A 59 @
[16] T [Z2]23 @
[17] A [T0]58 @
[18] A 59 @
[19] U [W3]31 @
[20] A [TWO]55 @
[21] T [W4]33 @        [/self-modified code]
[22]  [Z1] A 0 F        [swapping]
[23]  [Z2] S 0 F
[24]  G [SKIP-SWAP]34 @
[25]  T 61 @
[26]  A [TWO]55 @
[27]  T [F]62 @
[28]  [W1] A 0 F
[29]  T 61 @
[30]  [W2] A 0 F
[31]  [W3] T 0 F
[32]  A 61 @
[33]  [W4] T 0 F       [/swapping]
[34]  [SKIP-SWAP] T 61 @
[35]  [INCREMENT J] A 59 @
[36]   A [TWO]55 @
[37]   U 59 @
[38]   S 60 @
[39]   G [START]7 @
[40]   T 61 @
[41]   S [F]62 @
[42]   E [END]53 @
[43]   T 61 @
[44]   T [F]62 @
[45]   T 61 @
[46]   A 0 H  [adr = H]
[47]   T 59 @
[48]   A 60 @
[49]   S [TWO]55 @
[50]   U 60 @
[51]   S 59 @
[52]   E [START]7 @
[53]  [EXIT] T 62 @
[54]  [RET] E 0 F [EPILOG, RETURN FROM FUNC]
[CONSTS]
[55]  [TWO] P 1 F
[56]  [A0]  A 0 F
[57]  [S1]  S 1 F
[58]  [T0]  T 0 F
[59]  [J]   P 0 F
[60]  [I]   P 0 F
[61]  [TMP] P 0 F
[62]  [f]   P 0 F

[test routine]
G K
[0] X 0 F
[1] A [ADR] 8 @
[2] T 256 F [WRITE TO n]
[3] A [LEN] 9 @ [LEN]
[4] T 255 F [WRITE TO m]
[5] A 5 @            [CALL]
[6] G [SUB] 56 F  [SUBPROGRAMM]
[7] E 23 @ [GO TO SECOND TEST]   [Z 0 F]
[8] [ADR] P 13 @
[9] [LEN] P 10 F
[10] [SKIPS] P 0 F
P 0 F
P 0 F
[13] [ARRAY]P 1023 F
P 511 F
P 255 F
P 127 F
P 63 F
P 31 F
P 15 F
P 7 F
P 3 F
P 1 F
[23] A [ADR2]30 @
[24] T 256 F [WRITE ADR-2]
[25] A [LEN-2]31 @
[26] T 255 F [WRITE LEN-2]
[27] A 27 @
[28] G [SUB] 56 F
[29] Z 0 F [STOP]
[30] [ADR-2] P 32 @
[31] [LEN-2] P 8 F
P 7 F
P 31 F
P 1 F
P 255 F          [SORTED ARRAY] [ADR = 27@ , LEN 8]
P 156 F
P 1023 F
P 11 F
P 4095 F

EZ PF
