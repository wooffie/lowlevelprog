T 56 K [Адрес загрузки]

[adresses]
G K [ Директива, фиксация начального адреса]
T 45 K [ Установка адреса для параметров далее]
P 256 F [45 = H , тут будет адрес первого элемента массива]
P 255 F [46 = N , длинна массива для сортировки]
TZ  [ востановления адреса целевой ячейки]

[sub]
G K  [ Директива, фиксация начального адреса]
[далее в квадратных скобах адреса @ ]
[0] A 3 F [Пролог: формирование кода инструкции возврата]
[1] T 47 [RET] @   [Пролог: запись инструкции возврата]
[2]  A 0 H  [adr = H]   [set adr and len on 52,53]
[3]  U 52 @
[4]  A 0 N  [len = N]
[5]  S [TWO]48 @
[6]  T 53 @
[7] [START] T 54 @    [self-modified code]
[8]  A [A0]49 @
[9]  A 52 @
[10] U [Z1]22 @
[11] U [W1] 26 @
[12] A [TWO]48 @
[13] T [W2]28 @
[14] A [S1]50 @
[15] A 52 @
[16] T [Z2]23 @
[17] A [T0]51 @
[18] A 52 @
[19] U [W3]29 @
[20] A [TWO]48 @
[21] T [W4]31 @        [/self-modified code]
[22] [Z1] A 0 F        [swapping]
[23] [Z2] S 0 F
[24] G [SKIP-SWAP]32 @
[25] T 54 @
[26] [W1] A 0 F
[27] T 54 @
[28] [W2] A 0 F
[29] [W3] T 0 F
[30]  A 54 @
[31] [W4] T 0 F       [/swapping]
[32]  [SKIP-SWAP] T 54 @
[33]  [INCREMENT J] A 52 @
[34]   A [TWO]48 @
[35]   U 52 @
[36]   S 53 @
[37]   G [START]7 @
[38]   T 54 @
[39]    A 0 H  [adr = H]
[40]   T 52 @
[41]   A 53 @
[42]   S [TWO]48 @
[43]   U 53 @
[44]   S 52 @
[45]   E [START]7 @
[46]  [EXIT] T 54 @
[47]  [RET] E 0 F [EPILOG, RETURN FROM FUNC]
[CONSTS]
[48]  [TWO] P 1 F
[49]  [A0] A 0 F
[50]  [S1] S 1 F
[51]  [T0] T 0 F
[52]  [J] P 0 F
[53]  [I]  P 0 F
[54]  [TMP] P 0 F

[test routine]
G K
[0] X 0 F
[1] A [ADR] 8 @
[2] T 256 F [WRITE TO n]
[3] A [LEN] 9 @ [LEN]
[4] T 255 F [WRITE TO m]
[5] A 5 @            [CALL]
[6] G [SUB] 56 F  [SUBPROGRAMM]
[7] Z 0 F [STOP]
[8] [ADR] P 17 @
[9] [LEN] P 10 F
[10] [SKIPS] P 0 F
P 0 F
P 0 F
P 0 F
P 0 F
P 0 F
P 0 F
[17] [ARRAY]P 1023 F
P 511 F
P 255 F
P 127 F
P 63 F
P 31 F
P 15 F
P 7 F
P 3 F
P 1 F
EZ PF
